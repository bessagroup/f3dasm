!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Geometric Inputs!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!Wire Thickness
WThk = 0.3
!Wire thickness can be varied for this model from 0.1 to 0.49 i.e. 100 to 490 micrometers
 
!Foot Length 
FL = 0.5
!Foot length can be varied for this model from 0.5 to 1.2 i.e. 500 to 1200 micrometers 


!Loop Heights: (Limit 0.6 to 2.3)
L_H1 = 1.5 			!Loop Height of Loop Number 1
L_H2 = 1.5			!Loop Height of Loop Number 2
L_H3 = 1.5			!Loop Height of Loop Number 3

!Half Loop's Length:
!Assuming all the loops are symmetric:
L_L1 = 1.5			!Half Loop Length of Loop Number 1   !Min Limit: 1.5
L_L2 = 1.5			!Half Loop Length of Loop Number 2   !Min Limit: 0.6
L_L3 = 1.5			!Half Loop Length of Loop Number 3	 !Min Limit: 0.6

!Total Bond Wire Length Should Not Exceed 
!When L_L1 = L_L2 = L_L3 = 1.5, 
!then total wire length = Foot Lengths + All the Loop Lengths
!Here it would be: = (1*4)+ (1.5*2*3) = 13 mm

!This Should Not Be Exceeded:
!Minimum Wire Length = 	7.4 mm
!Maximum Wire Length =  16 mm    (FL + L_L1 + FL + L_L2 + FL + L_L3 + FL)


!Layer Thickness
L0 = 0 
L1 = 4.258			!Copper Baseplate 
L2 = 0.111			!Solder Layer 1 (SnAgCu)  		!Limits: 0.05 to 0.3
L3 = 0.283			!DCB Copper 1
L4 = 0.384			!Ceramic 
L5 = 0.283			!DCB Copper 2
L6 = 0.133			!Solder Layer 2 (SnAgCu)  		!Limits: 0.05 to 0.3
L7 = 0.283			!Silicon Chip             		!This can also be varied
L8 = 0.133			!Aluminum Metalization Layer 	!This can also be varied



!Foot_Offset_in_Y_Direction
FYO = L6+L7+L8

! Total number of layers in an IGBT
LayerCount=8

! Thickness of each layer (counted from bottom to top layer)
*del,LayerThk
*dim,LayerThk,ARRAY,LayerCount
LayerThk(1)= L1,L2,L3,L4,L5,L6,L7,L8


! Number of rectangular patches in each layer (counted from bottom to top layer)
*del,LayerAreaCount
*dim,LayerAreaCount,ARRAY,LayerCount

LayerAreaCount(1)=1, 1, 1, 1, 3, 1, 1, 1

*vscfun,Max_LayerAreaCount,MAX,LayerAreaCount

*del,KP_Count_Per_Area
*dim,KP_Count_Per_Area,ARRAY,Max_LayerAreaCount,LayerCount

KP_Count_Per_Area(1,1) = 4
KP_Count_Per_Area(1,2) = 4
KP_Count_Per_Area(1,3) = 4
KP_Count_Per_Area(1,4) = 4
KP_Count_Per_Area(1,5) = 4,4,4
KP_Count_Per_Area(1,6) = 4
KP_Count_Per_Area(1,7) = 4
KP_Count_Per_Area(1,8) = 4

*vscfun,Max_KP_Count,MAX,KP_Count_Per_Area(1,5)

*del,keypoints_X
*dim,keypoints_X,ARRAY,Max_KP_Count,Max_LayerAreaCount,LayerCount
*del,keypoints_Y
*dim,keypoints_Y,ARRAY,Max_KP_Count,Max_LayerAreaCount,LayerCount
*del,keypoints_Z
*dim,keypoints_Z,ARRAY,Max_KP_Count,Max_LayerAreaCount,LayerCount


!Keypoints in Layer 1 Area 1
keypoints_X(1,1,1) = 0,1,1,0
keypoints_Y(1,1,1) = 0,0,32,32
keypoints_Z(1,1,1) = L0,L0,L0,L0

!Keypoints in Layer 2 Area 1
keypoints_X(1,1,2) = 0,1,1,0
keypoints_Y(1,1,2) = 2.5,2.5,29.5,29.5
keypoints_Z(1,1,2) = L0,L0,L0,L0

!Keypoints in Layer 3 Area 1
keypoints_X(1,1,3) = 0,1,1,0
keypoints_Y(1,1,3) = 2.5,2.5,29.5,29.5
keypoints_Z(1,1,3) = L0,L0,L0,L0

!Keypoints in Layer 4 Area 1
keypoints_X(1,1,4) = 0,1,1,0
keypoints_Y(1,1,4) = 2,2,30,30
keypoints_Z(1,1,4) = L0,L0,L0,L0

!Keypoints in Layer 5 Area 1
keypoints_X(1,1,5) = 0,1,1,0
keypoints_Y(1,1,5) = 26.45,26.45,30,30
keypoints_Z(1,1,5) = L0,L0,L0,L0

!Keypoints in Layer 5 Area 2
keypoints_X(1,2,5) = 0,1,1,0
keypoints_Y(1,2,5) = 6.5,6.5,25.5,25.5
keypoints_Z(1,2,5) = L0,L0,L0,L0

!Keypoints in Layer 5 Area 3
keypoints_X(1,3,5) = 0,1,1,0
keypoints_Y(1,3,5) = 2,2,5.55,5.55
keypoints_Z(1,3,5) = L0,L0,L0,L0

!Keypoints in Layer 6 Area 1
keypoints_X(1,1,6) = 0,1,1,0
keypoints_Y(1,1,6) = 7.45,7.45,19.58,19.58
keypoints_Z(1,1,6) = L0,L0,L0,L0

!Keypoints in Layer 7 Area 1
keypoints_X(1,1,7) = 0,1,1,0
keypoints_Y(1,1,7) = 7.45,7.45,19.58,19.58
keypoints_Z(1,1,7) = L0,L0,L0,L0

!Keypoints in Layer 8 Area 1
keypoints_X(1,1,8) = 0,1,1,0
keypoints_Y(1,1,8) = 7.45,7.45,19.58,19.58
keypoints_Z(1,1,8) = L0,L0,L0,L0

/INPUT,02_Mesh_Parameters,txt,,,
                                                                                              !

!Layer Level
LL0 = L0
LL1 = L0+L1
LL2 = L0+L1+L2
LL3 = L0+L1+L2+L3
LL4 = L0+L1+L2+L3+L4
LL5 = L0+L1+L2+L3+L4+L5
LL6 = L0+L1+L2+L3+L4+L5+L6
LL7 = L0+L1+L2+L3+L4+L5+L6+L7

*del,LayerLevel
*dim,LayerLevel,ARRAY,LayerCount
LayerLevel(1) = LL0,LL1,LL2,LL3,LL4,LL5,LL6,LL7


/PREP7
                                                                                                !!
ET,1,185 
ET,2,PLANE182
SELTOL,0.0005
                                                                                     !!

! Loop through each layer:
*do,nthLayer,1,LayerCount,1

    ! Get current layer attributes (number of rectangles, thickness)
    AreaCount=LayerAreaCount(nthLayer)
    !thk=LayerThk(nthLayer+1)

    ! Loop through each areas:
    *do,nthAreas,1,AreaCount,1

        ! Get current Area attributes (number of areas, Keypoints)
        KP_Count=KP_Count_Per_Area(nthAreas,nthLayer)

        *del,kplist
        *dim,kplist,array,KP_Count+1

        *do,nthKP,1,KP_Count,1
            *get,kmaxd,kp,,num,maxd
            knew=kmaxd+1
            xLLC=keypoints_X(nthKP,nthAreas,nthLayer)
            yLLC=keypoints_Y(nthKP,nthAreas,nthLayer)
            zLLC=keypoints_Z(nthKP,nthAreas,nthLayer)
            k,knew,xLLC,yLLC,zLLC,
            kplist(nthKP)=knew
        *enddo

        kplist(KP_Count+1)=kplist(1)
        lsel,u,line,,all

        *do,nthLine,1,KP_Count
            k1=kplist(nthLine)
            k2=kplist(nthLine+1)
            l,k1,k2
        *enddo

        cm,compl_perimeter,line
        al,compl_perimeter

    *enddo
	CM,IGBT_Layer%nthLayer%,AREA
*enddo


/INPUT,02_Areas_of_foot_base,txt,,,

!Select All Created Areas: 
asel,s,loc,z,0

!Substrat All Selected Areas From Each Other:
aptn,all

lsel,s,loc,z,0
lsel,r,length,,WThk
lesize,all,,,NEF1

lsel,s,loc,z,0
lsel,r,length,,FL
lsel,u,loc,y,0,3
lsel,u,loc,y,5.4,7.6
lsel,u,loc,y,18.7,35
CM,Foot_Lines,Line
lesize,all,,,NEF2

lsel,s,loc,z,0
!lsel,u,length,,FL
lsel,u,length,,WThk
CMSEL,U,Foot_Lines,Line
lesize,all,MeshSize_XY

!Mesh All The Created Areas:
asel,s,loc,z,0
amesh,all

lsel,s,length,,WThk
lsel,a,length,,FL
asll,s,1
esla,s
CM,Foot_Areas,ELEM
/COLOR,CM,ORAN,Foot_Areas



!Extrude the Areas for Each Layer:
*do,nthLayer,1,LayerCount,1
    vsel,u,volu,,all
    thk=LayerThk(nthLayer)
    asel,s,loc,z,LayerLevel(nthLayer)
	esize,,MeshDiv_thk(nthLayer)
    vext,all,,,,,thk
    cvName=STRCAT('COMPV_L',CHRVAL(nthLayer))
    CM,%cvName%,VOLU
    cnum=MOD(nthLayer,11)+2
    /color,CM,cnum,cvName
*enddo

!Layer 1
esel,s,cent,z,LL0,LL1
CM,L_1,ELEM
/COLOR,CM,ORAN,L_1

!Layer 2
asel,s,loc,z,LL1,LL2
asel,r,loc,y,2.5,29.5
vsla,s,1
eslv,s
CM,L_2,ELEM
/COLOR,CM,MRED,L_2

!Layer 3
asel,s,loc,z,LL2,LL3
asel,r,loc,y,2.5,29.5
vsla,s,1
eslv,s
CM,L_3,ELEM
/COLOR,CM,ORAN,L_3

!Layer 4
asel,s,loc,z,LL3,LL4
asel,r,loc,y,2,30
vsla,s,1
eslv,s
CM,L_4,ELEM
/COLOR,CM,LGRA,L_4

!Layer 5
asel,s,loc,z,LL4,LL5
asel,r,loc,y,2,5.55
vsla,s,1
eslv,s

asel,s,loc,z,LL4,LL5
asel,r,loc,y,6.5,25.5
vsla,s,1
eslv,a

asel,s,loc,z,LL4,LL5
asel,r,loc,y,26.45,30
vsla,s,1
eslv,a

CM,L_5,ELEM
/COLOR,CM,ORAN,L_5

!Layer 6
asel,s,loc,z,LL5,LL6
asel,r,loc,y,7.45,19.58
vsla,s,1
eslv,s
CM,L_6,ELEM
/COLOR,CM,LGRA,L_6

!Layer 7
asel,s,loc,z,LL6,LL7
asel,r,loc,y,7.45,19.58
vsla,s,1
eslv,s
CM,L_7,ELEM
/COLOR,CM,GREE,L_7

!Layer 8
asel,s,loc,z,LL7,LL7+L8
asel,r,loc,y,7.45,19.58
vsla,s,1
eslv,s
CM,L_8,ELEM
/COLOR,CM,DGRA,L_8


!Deleting the extra volumes in the layers
SELTOL,0.0005
vsel,s,loc,z,LL1,LL2
asel,s,loc,z,LL1,LL2
asel,r,loc,y,2.5,29.5
vsla,u,1
vclear,all
vdele,all

vsel,s,loc,z,LL2,LL3
asel,s,loc,z,LL2,LL3
asel,r,loc,y,2.5,29.5
vsla,u,1
vclear,all
vdele,all

vsel,s,loc,z,LL3,LL4
asel,s,loc,z,LL3,LL4
asel,r,loc,y,2,30
vsla,u,1
vclear,all
vdele,all

vsel,s,loc,z,LL4,LL5
asel,s,loc,z,LL4,LL5
asel,r,loc,y,2,5.55
vsla,u,1

asel,s,loc,z,LL4,LL5
asel,r,loc,y,6.5,25.5
vsla,u,1

asel,s,loc,z,LL4,LL5
asel,r,loc,y,26.45,30
vsla,u,1
vclear,all
vdele,all

vsel,s,loc,z,LL5,LL7+L8
asel,s,loc,z,LL5,LL7+L8
asel,r,loc,y,7.45,19.58
vsla,u,1
vclear,all
vdele,all


/INPUT,03_Wire_Parametric,txt,,,

/INPUT,17_Material_Data_IGBT,txt,,,

!Defining Materials for Module Layers: 

CMSEL,S,L_1,ELEM
MPCHG,mat_Cu_Baseplate,ALL

CMSEL,S,L_2,ELEM
MPCHG,mat_Solder1,ALL

CMSEL,S,L_3,ELEM
CMSEL,A,L_5,ELEM
MPCHG,mat_Cu_DCB,ALL

CMSEL,S,L_4,ELEM
MPCHG,mat_DCB_Ceramic,ALL

CMSEL,S,L_6,ELEM
MPCHG,mat_Solder2,ALL

CMSEL,S,L_7,ELEM
MPCHG,mat_Si_Chip,ALL

CMSEL,S,L_8,ELEM
MPCHG,mat_AL_Pad,ALL

CMSEL,S,wire,ELEM
MPCHG,mat_Al_Wire,ALL


alls
save

/INPUT,18_BC_and_Solution,txt,,,

/INPUT,21_Postprocessing,txt,,,







