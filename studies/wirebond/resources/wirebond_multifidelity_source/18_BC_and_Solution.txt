alls
asel,all
aclear,all

!!!!!!!!!!!!!!!!!!!!!!Defining Contacts!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!TARGET / CONTACT!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

mcontact = 24 $ matn=24 	!MPC Contact (actually a permanently sticking glue)
mp,mu,matn,1            	!Friction Coefficient = 1
mifssm = 46   $ matn=46   	!InterFace between Solder and SolderMask
mp,mu,matn,0            	!Friction coefficient = 0 (= no friction, free sliding)


et,12,170,,,,,     			!3-D Target (Solid-Solid constraint - no rotational DOFs)

et,13,174,,2,,2    			!quad.3-D Contact Element - MPC mode, Force distributed surf)

KEYOPT,13,12,5  			!keyopt(12)=5, bonded (always)


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!Real Constants!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! Note: Real Constant 1 is the default value. 
!       Real Constants 2...10 are used auxiliarily during mesh generation.
!       Thus, actual Real Constants should use numbers greater 10.

!! Contact Real Constant sets
! Contact Real Constants: Multi Point Contact (MPC) 
rcmpc=12                ! RealConstant for the MultiPointConstraint contanct
r,rcmpc,,,,,2e-3,-1e-3  ! MPC RC set (1=R1,2=R2,3=fkn,4=ftol,5=ICONT,6=PINB)
rmore,-5e-4,-1e-15      ! ... Pos. 7-12 (7=PMAX,8=PMIN,9=TauMax,10=Cnof,11=Fkop)


! Contact between solder and substr. solder mask (BGA3.1: individual nodes, gap)
rcsolsm=101         ! Real Constant for contact between SOLder and Solder Mask
!r,rcsolsm,,,1,1e-2  ! RC set (1=R1,2=R2,3=FKN,4=FTOL,5=iCont,6=Pinb) too weak?
r,rcsolsm,,,5,1e-2  ! RC set (1=R1,2=R2,3=FKN,4=FTOL,5=iCont,6=Pinb)
rmore,              ! ... Pos. 7-12 (7=Pmax,8=Pmin,9=TauMax,10=Cnof,11=Fkop)


WPCSYS,1,0 $ CSYS,4

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Boundary Conditions!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!Node 1 at 1st corner
nsel,s,loc,z,0
nsel,r,loc,x,0
nsel,r,loc,y,0
CM,N1,Node
d,all,uz,0

!Node 2 at 2nd corner
nsel,s,loc,z,0
nsel,r,loc,x,1
nsel,r,loc,y,0
CM,N2,Node
d,all,ux,0
d,all,uy,0
d,all,uz,0

!Node 3 at 3rd corner
nsel,s,loc,z,0
nsel,r,loc,x,1
nsel,r,loc,y,32
CM,N3,Node
d,all,ux,0
d,all,uz,0



/com,****************************************************************************
/com,*************************    SOLUTION       ********************************
/com,****************************************************************************

alls
save


/SOLU


    ALLS
    antype,static,new 
    NLGEOM,ON       !LARGE DEFORMATION (NON-LINEAR)
    DSPOPT,INCORE   ! USE INCORE MEMORY IF POSSIBLE
    autots,on		! auto
    timint,off		! no mass or inertia
    solcontrol,on
    nropt,full   	        ! explicitly set the Newton-Raphson option
    rate,on			! include creep effects
    cutc,crpl,5,1 ! Max.ratio (=5) allowed for IMPLICIT (=1) creep in each substep

    KBC,0
	outres,all,last
	NEQIT, 60
	
	!** LOAD STEP TEMPERATURES (IN K)
    Stress_free_TEMP = 200+273     
	Troom= 22+273 		! [oC] minimum temperature at which warpage is measured
	T_max=125+273 
	T_min=-40+273 
	
	Ramp_up=15		!20, 6, 2 °C/min
	Ramp_up1=Ramp_up/60	  		!°C/sec
	Ramp_up2=0.65*Ramp_up1
	Ramp_up3=0.65*Ramp_up2
	Ramp_up4=0.75*Ramp_up3
	
	Ramp_down=15		!20, 6, 2 °C/min
	Ramp_down1=Ramp_down/60			!°C/sec
	Ramp_down2=0.65*Ramp_down1
	Ramp_down3=0.65*Ramp_down2
	Ramp_down4=0.75*Ramp_down3

	th_dwell=900		!60, 600, 6000 sec
	tl_dwell=900		!60, 600, 6000 sec
	
	NSEL,ALL                   ! Select all nodes
	TREF,Stress_free_TEMP		! reference T for CTE is set to 125 °C
	TOFFST,0
	TUNIF,Stress_free_TEMP		! set all nodes unified T equal to T_ref

  /COM, ---------------------------------------------------
  /COM,  Temperature cycles
  /COM, ---------------------------------------------------
!

	num_step=10
	tm=600/num_step
	delta_T=Stress_free_TEMP-Troom
	temp=delta_T/num_step
	*do,step,1,num_step,1
		BF,all,TEMP,Stress_free_TEMP-60-temp*step
		time,TIMETOT + tm*step
		nsubst,5,200,2,on
		SOLVE
	*enddo
    TIMETOT = 600
	save

	
	!LOAD STEP   ************* Dwelling at Troom C *********
    TIMETOT = TIMETOT + 600000
    TIME, TIMETOT	! 1 week
	nsubst,10,20000,5,on
    SOLVE
	SAVE

	delta_T=(Troom-T_min)
	tm=delta_T/Ramp_down1
	*if,tm,lt,0,then
		tm=-tm
	*endif
	BF,all,TEMP,(Troom+T_min)/2
	time,TIMETOT + tm/2
	nsubst,2,100,2,on
	SOLVE
	BF,all,TEMP,T_min
	time,TIMETOT + tm
	nsubst,2,100,2,on
	SOLVE		
    TIMETOT = TIMETOT + tm
	
    !LOAD STEP   ************* Dwelling at Tmin C *********
    TIMETOT = TIMETOT + tl_dwell 
    TIME, TIMETOT
	nsubst,5,200,2,on
    SOLVE
	SAVE

	
	delta_T=T_max-T_min
	
	/INPUT,20_T_heat,mac,,,
	

	!LOAD STEP   ************* Dwelling at Tmax C *********
    TIMETOT = TIMETOT + th_dwell
    TIME, TIMETOT
	nsubst,5,200,2,on
    SOLVE
    SAVE

	/INPUT,19_T_cool,mac,,,
	
	!LOAD STEP   ************* Dwelling at Tmin C *********
    TIMETOT = TIMETOT + tl_dwell
    TIME, TIMETOT
	nsubst,5,20,2,on
    SOLVE
    SAVE
	
	/INPUT,20_T_heat,mac,,,

	!LOAD STEP   ************* Dwelling at Tmax C *********
    TIMETOT = TIMETOT + th_dwell
    TIME, TIMETOT
	nsubst,5,200,2,on
    SOLVE
    SAVE
	
	/INPUT,19_T_cool,mac,,,
	
	!LOAD STEP   ************* Dwelling at Tmin C *********
    TIMETOT = TIMETOT + tl_dwell
    TIME, TIMETOT
	nsubst,5,20,2,on
    SOLVE
    SAVE

alls
save
	